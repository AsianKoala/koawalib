
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

group = 'com.github.asiankoala'

android {
    compileSdk 29

    defaultConfig {
        minSdk 24
        targetSdk 29
        versionCode 1
        versionName "1.0"

//        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

afterEvaluate {
    publishing {
        publications {
            productionRelease(MavenPublication) {
                from components.release
                groupId = 'com.github.asiankoala'
                artifactId = 'koawalib'
                version = '1.0.0'
            }
        }
    }
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
    generateReleaseResValues.enabled = false
    generateDebugResValues.enabled = false
}

configurations {
    ktlint
}

dependencies {
    repositories {
        maven { url = 'https://maven.brott.dev/' }
        maven { url = 'https://jitpack.io' }
        maven { url "https://kotlin.bintray.com/kotlin-datascience" }
    }

    //noinspection GradleDependency
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    compileOnly 'org.jetbrains.lets-plot:lets-plot-kotlin-jvm:3.1.1'
    compileOnly 'org.jetbrains.lets-plot:lets-plot-image-export:2.1.0'
    implementation 'org.firstinspires.ftc:RobotCore:8.0.0'
    implementation 'org.ejml:ejml-all:0.41'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation 'com.acmerobotics.dashboard:dashboard:0.4.7'
    implementation 'com.github.Eeshwar-Krishnan:PhotonFTC:v2.0.1'
    compileOnly 'org.openftc:easyopencv:1.5.3'

    ktlint("com.pinterest:ktlint:0.42.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "**/*.kt"
}

tasks.withType(dokkaHtml.getClass()).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{ "separateInheritedMembers": true}"""]
    )
}

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../docs/"))
}

